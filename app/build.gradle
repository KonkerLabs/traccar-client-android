apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.konker.corona.client"
        buildConfigField "boolean", "HIDDEN_APP", "false"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 55
        versionName '5.18'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "default"
    productFlavors {
        regular
        google
        hidden {
            applicationId "org.konker.corona.client.hidden"
            buildConfigField "boolean", "HIDDEN_APP", "true"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    buildToolsVersion = '28.0.3'
}

dependencies {
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    googleImplementation 'com.google.firebase:firebase-core:17.3.0'
    googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    googleImplementation 'com.google.android.gms:play-services-location:17.0.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'

    task copyJson(type: Copy) {
        from '../../traccar-shared/google-services'
        into '.'
        include 'traccar-client-android.json'
        rename('traccar-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
